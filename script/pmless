#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{pmless} = {
    v => 1.1,
    summary => 'Show Perl module source code with `less`',
    args => {
        modules => $App::PMUtils::arg_module_multiple,
        pod => {
            schema => 'bool',
            default => 0,
        },
    },
    deps => {
        prog => 'less',
    },
};
sub pmless {
    require Module::Path::More;
    my %args = @_;
    my $mods = $args{modules};
    my @mpaths;
    for my $mod (@$mods) {
        my $mpath = Module::Path::More::module_path(
            module => $mod, find_pmc=>0, find_pod=>$args{pod}, find_prefix=>0);
        if (defined $mpath) {
            push @mpaths, $mpath;
        } else {
            warn "pmless: Can't find path for module '$mod'\n";
        }
    }
    return [404, "Can't find path(s) for module(s)"] unless @mpaths;
    system "less", @mpaths;
    [200, "OK"];
}

Perinci::CmdLine::Any->new(
    url => '/main/pmless',
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

Basic usage:

 % pmless Some::Module
