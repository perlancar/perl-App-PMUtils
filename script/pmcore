#!perl

use 5.010;
use strict;
use warnings;
use Log::ger;

use App::PMUtils;
use Perinci::CmdLine::Any;

# AUTHORITY
# DATE
# DIST
# VERSION

our %SPEC;
$SPEC{pmcore} = {
    v => 1.1,
    summary => 'Check if Perl module is in core',
    description => <<'_',



_
    args => {
        module => $App::PMUtils::arg_module_single,
        modver => {
            schema => 'str*',
            pos    => 1,
            # XXX complete using Module::Corelist
        },
        perlver => {
            schema => 'str*',
            'x.schema.entity' => 'perl_version',
        },
    },
};
sub pmcore {
    require Module::CoreList;

    my %args = @_;
    my $mod = $args{module};
    $mod =~ s/\.pm$//; $mod =~ s!/!::!g; # convenience
    my $modver = $args{modver};
    my $perlver = $args{perlver};

    my $modv = "$mod".($modver ? " version $modver" : "");

    my $ans;
    if ($perlver) {
        $ans = Module::CoreList->is_core($mod, $modver,
                                         version->parse($perlver));
        if ($ans) {
            return [200, "OK", 1, {
                "cmdline.result"=>"$modv was in core perl $perlver",
                "cmdline.exit_code" => 0,
            }];
        } else {
            return [200, "OK", 0, {
                "cmdline.result"=>"$modv was NOT in core perl $perlver",
                "cmdline.exit_code" => 1,
            }];
        }
    } else {
        if ($modver) {
            $ans = Module::CoreList->first_release($mod, $modver);
        } else {
            $ans = Module::CoreList->first_release($mod);
        }
        if ($ans) {
            return [200, "OK", 1, {
                "cmdline.result"=>"$modv was first released with perl $ans",
                "cmdline.exit_code" => 0,
            }];
        } else {
            return [200, "OK", 1, {
                "cmdline.result"=>"$modv was not in CORE (or so I think)",
                "cmdline.exit_code" => 1,
            }];
        }
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/pmcore',
    read_config => 0,
    read_env => 0,
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

 % pmcore MIME::Base64
 MIME::Base64 was first released with perl v5.7.3

 % pmcore Foo
 Foo was not in CORE (or so I think)
