#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;
use Log::Any '$log';

use Perinci::CmdLine 1.04;

# DATE
# VERSION

our %SPEC;
$SPEC{pminfo} = {
    v => 1.1,
    summary => 'Get Perl module information',
    description => <<'_',

Will show this information about each Perl module:

* path
* version
* number of lines of code
*

_
    args => {
        module => {
            schema => ['array*' => of => 'str*', min_len=>1],
            req => 1,
            pos => 0,
            greedy => 1,
            element_completion => sub {
                require Complete::Module;
                require Complete::Util;
                my %args = @_;
                Complete::Util::mimic_shell_dir_completion(
                    completion => Complete::Module::complete_module(
                        word      => $args{word},
                        find_pmc  => 0,
                        find_pod  => 0,
                        separator => '/',
                        ci        => 1,
                    )
                );
            },
        },
    },
    result_naked => 1,
};
sub pminfo {
    require Module::Load;
    require SHARYANTO::Module::Path;

    my %args = @_;
    my $mods = $args{module};

    my $res = [];
    for my $mod (@$mods) {
        $mod =~ s!/!::!g;
        my $rec = {module=>$mod};
        push @$res, $rec;
        my $mpath = SHARYANTO::Module::Path::module_path(module=>$mod);
        $rec->{path} = $mpath;
        next unless $mpath;
        # load to get $VERSION & $DATE
        {
            no strict 'refs';
            Module::Load::load($mod);
            $rec->{version} = ${"$mod\::VERSION"};
            $rec->{date} = ${"$mod\::DATE"};
        }
        if (open my($fh), "<", $mpath) {
            my $lines = 0; $lines++ while <$fh>;
            $rec->{lines} = $lines;
        }
    }

    $res;
}

Perinci::CmdLine->new(url => '/main/pminfo')->run;

# ABSTRACT: Get Perl module information
# PODNAME: pminfo

=head1 SYNOPSIS

 % pminfo Some::Module Another::Module

To active bash completion:

 % complete -C pminfo pminfo ; # can be put in bash startup file e.g. .bashrc
 % pminfo Some<tab>

