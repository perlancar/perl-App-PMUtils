#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine 1.04;

# DATE
# VERSION

our %SPEC;
$SPEC{pmless} = {
    v => 1.1,
    summary => 'Show Perl module source code with `less`',
    args => {
        module => {
            schema => 'str*',
            req => 1,
            pos => 0,
            completion => sub {
                require Complete::Module;
                require Complete::Util;
                my %args = @_;
                Complete::Util::mimic_shell_dir_completion(
                    completion => Complete::Module::complete_module(
                        word      => $args{word},
                        find_pmc  => 0,
                        find_pod  => 0,
                        ci        => 1,
                    )
                );
            },
        },
    },
    deps => {
        prog => 'less',
    },
};
sub pmless {
    require SHARYANTO::Module::Path;
    my %args = @_;
    my $mod = $args{module};
    my $mpath = SHARYANTO::Module::Path::module_path(
        module => $mod, find_pmc=>0, find_pod=>0, find_prefix=>0);
    if (defined $mpath) {
        system "less", $mpath;
        [200, "OK"];
    } else {
        [404, "Can't find module $mod"];
    }
}

Perinci::CmdLine->new(url => '/main/pmless')->run;

# ABSTRACT: Show Perl module source code with `less`
# PODNAME: pmless-colon

=head1 SYNOPSIS

Basic usage:

 % pmless-colon Some::Module

To active bash completion:

 % complete -C pmless-colon pmless-colon ; # can be put in bash startup file e.g. .bashrc
 % pmless-colon Some<tab>


=head1 DESCRIPTION

Just like L<pmless>, except it completes module names using the standard
double-colon for namespace separator. For testing.
