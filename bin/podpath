#!perl

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any -prefer_lite=>1;

# VERSION
# DATE

our %SPEC;
$SPEC{podpath} = {
    v => 1.1,
    summary => 'Get path to locally installed POD',
    args => {
        pod => {
            schema => ['array*', of=>'str*', min_len=>1],
            req    => 1,
            pos    => 0,
            greedy => 1,
            element_completion => $App::PMUtils::_complete_pod,
        },
        all => {
            schema => 'bool',
            cmdline_aliases => {a=>{}},
        },
        abs => {
            schema => 'bool',
            cmdline_aliases => {P=>{}},
        },
    },
    "cmdline.default_format" => 'text-simple',
};
sub podpath {
    require SHARYANTO::Module::Path;
    my %args = @_;

    my $pods = $args{pod};
    my $res = [];
    my $found;

    for my $pod (@{$pods}) {
        my $mpath = SHARYANTO::Module::Path::module_path(
            module=>$pod, find_pmc=>0, find_pm=>0, find_pod=>1, find_prefix=>0,
            abs=>$args{abs}, all=>$args{all});
        $found++ if $mpath;
        push @$res, @$pods > 1 ? {pod=>$pod, path=>$mpath} : $mpath;
    }

    if ($found) {
        [200, "OK", $res];
    } else {
        [404, "No such POD"];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/podpath',
)->run;

# ABSTRACT: Get path to locally installed POD
# PODNAME: podpath

=head1 SYNOPSIS

Basic usage:

 % podpath PSGI

