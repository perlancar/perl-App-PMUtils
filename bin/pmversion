#!perl

use 5.010;
use strict;
use warnings;
use Log::Any '$log';

use App::PMUtils;
use Perinci::CmdLine::Any -prefer_lite=>1;

# DATE
# VERSION

our %SPEC;
$SPEC{pmversion} = {
    v => 1.1,
    summary => 'Get Perl module version',
    description => <<'_',
_
    args => {
        module => {
            schema => ['array*' => of => 'str*', min_len=>1],
            req => 1,
            pos => 0,
            greedy => 1,
            element_completion => $App::PMUtils::_complete_module,
        },
    },
};
sub pmversion {
    require Module::Load;
    require Module::Path::More;

    my %args = @_;
    my $mods = $args{module};

    my $found;
    my $res = [];
    for my $mod (@$mods) {
        $mod =~ s!/!::!g;
        my $mpath = Module::Path::More::module_path(module=>$mod);
        unless ($mpath) {
            push @$res, "Module $mod is not installed";
            next;
        }
        $found++;
        # load to get $VERSION & $DATE
        {
            no strict 'refs';
            Module::Load::load($mod);
            my $v = ${"$mod\::VERSION"};
            if (defined $v) {
                push @$res, (@$mods > 1 ? "$mod $v" : $v);
            } else {
                push @$res, "Module $mod does not define \$VERSION";
            }
        }
    }

    if (!$found) {
        [404, "No such module(s): " . join(", ", @$mods)];
    } else {
        [200, "OK", $res];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/pmversion',
)->run;

# ABSTRACT: Get Perl module version
# PODNAME:

=head1 SYNOPSIS

 % pmversion Some::Module Another::Module


=head1 SEE ALSO

L<pminfo>

