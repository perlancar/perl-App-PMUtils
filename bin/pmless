#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine::Any -prefer_lite=>1;

our %SPEC;
$SPEC{pmless} = {
    v => 1.1,
    summary => 'Show Perl module source code with `less`',
    args => {
        module => {
            schema => 'str*',
            req => 1,
            pos => 0,
            completion => sub {
                require Complete::Module;
                my %args = @_;
                Complete::Module::complete_module(word=>$args{word});
            },
        },
    },
    deps => {
        prog => 'less',
    },
};
sub pmless {
    require Module::Path::More;
    my %args = @_;
    my $mod = $args{module};
    my $mpath = Module::Path::More::module_path(
        module => $mod, find_pmc=>0, find_pod=>0, find_prefix=>0);
    if (defined $mpath) {
        system "less", $mpath;
        [200, "OK"];
    } else {
        [404, "Can't find module $mod"];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/pmless',
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

Basic usage:

 % pmless Some::Module

